
val String filename = "kodi.rules"

rule "Kodi init"
when
    System started
then
    createTimer(now.plusSeconds(175)) [|
        //if (Kodi_Title_Show.state == NULL) Kodi_Title_Show.postUpdate("")
        //if (Kodi_Title.state == NULL) Kodi_Title.postUpdate("")
        //if (Kodi_Player_State.state == NULL) Kodi_Player_State.postUpdate("Stop")
        if (Kodi_System_State.state == NULL) Kodi_System_State.postUpdate(OFF)
        if (Kodi_Summary.state == NULL || Kodi_Summary.state == "") Kodi_Summary.postUpdate("(unbekannt) ⁉")
    ]
end

rule "Kodi Summary update"
when
    Item Kodi_System_State changed or
    Item Kodi_Player_State changed or
    Item Kodi_Title changed
then
    //logInfo(filename, "--> Kodi Summary update")
    if (Kodi_System_State.state == NULL || Kodi_Player_State.state == NULL || Kodi_Title.state == NULL) return

    val String status = if (Kodi_Player_State.state == "Play") "⏯" else if (Kodi_Player_State.state == "Pause") "⏸" else "⏹"
    val String title = (Kodi_Title.state as StringType).toString
    val String show = (Kodi_Title_Show.state as StringType).toString

    var String summary = //
        if (Kodi_System_State.state == OFF)           "nicht aktiv\u2000✘"
        else if (Kodi_Player_State.state == "Stop")   "inaktiv\u2000⏹"
        else if (Kodi_Title_Show.state == "")         String::format("%s\u2000%s", title, status)
        else                                        String::format("%s: %s\u2000%s", show, title, status)

    Kodi_Summary.postUpdate(summary)
end


rule "Kodi reset PlayPause controls"
when
    Item Kodi_PlayPause received command
then
    //logInfo(filename, "Kodi reset PlayPause controls")
    Kodi_PlayPause.postUpdate(OFF)
end

rule "Kodi Notification triggered"
when
    Item Kodi_NotificationId received command
then
    //logInfo(filename, "Kodi Notification triggered")
    var String msg = "((empty...))"
    switch (receivedCommand) {
        case 1: msg = "Hello! :)"
        case 2: msg = "Stop being lazy!"
        case 3: msg = "Go to bed! :P"
        case 4: msg = "I love this show!!!"
        case 5: msg = "Meh."
        case 6: msg = "Garbage! :O"
    }

    Kodi_NotificationText.sendCommand(msg)
    createTimer(now.plusSeconds(3)) [|
        //Kodi_NotificationText.sendCommand("")
        Kodi_NotificationId.postUpdate(-1)
    ]
end

rule "Sound System: Switch between PC input and Sonos input channel"
when
    Item Kodi_Player_State changed
then
    //logInfo(filename, "Sound System: Switch between PC input and Sonos input channel")
    if (Kodi_Player_State.state == "Play") WZ_zMote_sendcode.sendCommand("38000,2,69,343,171,21,22,21,65BBBBBBBBBBBCBCBBCBBBBBCCBCCCCC21,1672,343,86,21,3730")
    else if (Kodi_Player_State.state == "Stop") WZ_zMote_sendcode.sendCommand("38000,2,69,343,171,21,22,21,65BBBBBBBBBBBCBCBCBBBBBCCBCCCCCB21,1672,343,86,21,3730")
end

// vim: syntax=Xtend
