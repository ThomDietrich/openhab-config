
val String filename = "lichtprogramm.rules"


rule "Schranktaster Arbeitsbeleuchtung ON"
when
    Item FK_Taster_Schrank_2Short changed to ON
then
    if (FK_Arbeit.state == OFF) {
        FK_Arbeit.sendCommand(ON)
    } else if (FK_Edison_Dimmer_Level.state != 70) {
        FK_Edison_Dimmer_Level.sendCommand(70)
    } else {
        Kueche_Deckenlicht.sendCommand(ON)
    }
end


rule "Schranktaster Arbeitsbeleuchtung OFF"
when
    Item FK_Taster_Schrank_1Short changed to ON
then
    FK_Arbeit.sendCommand(OFF)
    Kueche_Deckenlicht.sendCommand(OFF)
    if (FK_Edison_Dimmer_Level.state == 30) FK_Edison_Dimmer_Level.sendCommand(OFF)
    if (FK_Edison_Dimmer_Level.state == 70) FK_Edison_Dimmer_Level.sendCommand(30)
end


rule "Schranktaster Hauptleuchte ON"  when Item FK_Taster_Schrank_2Long changed to ON then Kueche_Deckenlicht.sendCommand(ON) end
rule "Schranktaster Hauptleuchte OFF" when Item FK_Taster_Schrank_1Long changed to ON then Kueche_Deckenlicht.sendCommand(OFF) end


// Wohnzimmer Taster Eingang
rule "Wohnzimmer Taster oben kurz"
  when Item WZ_Taster_2Short changed to ON
  then Whg_Light_Preset.sendCommand(2)
end
rule "Wohnzimmer Taster unten kurz"
  when Item WZ_Taster_1Short changed to ON
  then Whg_Light_Preset.sendCommand(0)
end


rule "Whg_Light_Preset (Sitemap -> Homematic)"
when
  Item Whg_Light_Preset received command
then
    //logInfo(filename, "Whg_Light_Preset (Sitemap -> Homematic).")
    switch (receivedCommand){
        case 0: {
            Lights.members.filter[item | item.state != OFF].forEach[item | item.sendCommand(OFF)]
            logInfo(filename, "Lichtprogramm 0 - Aus")
        }
        case 1: {
            logInfo(filename, "Lichtprogramm 1 - Normal")
            BK_Balkonmagic.sendCommand(ON)
            WZ_Lacks_LEDs.sendCommand(ON)
            WZ_Origami.sendCommand(ON)
            WZ_Expedit.sendCommand(ON)
            FK_Arbeit.sendCommand(ON)
            FK_Edison_Dimmer_Level.sendCommand(70)
            WZ_Goldfruchtpalme.sendCommand(OFF)
            WZ_Lacks_Lampe.sendCommand(OFF)
            Flur_Deckenlicht.sendCommand(OFF)
            Kueche_Deckenlicht.sendCommand(OFF)
            Lights.members.filter[item | item.state == NULL].forEach[item | item.sendCommand(OFF)]
        }
        case 2: {
            logInfo(filename, "Lichtprogramm 2 - Relax")
            BK_Balkonmagic.sendCommand(ON)
            WZ_Lacks_LEDs.sendCommand(ON)
            WZ_Origami.sendCommand(ON)
            WZ_Expedit.sendCommand(ON)
            FK_Arbeit.sendCommand(ON)
            FK_Edison_Dimmer_Level.sendCommand(30)
            WZ_Goldfruchtpalme.sendCommand(OFF)
            WZ_Lacks_Lampe.sendCommand(OFF)
            Flur_Deckenlicht.sendCommand(OFF)
            Kueche_Deckenlicht.sendCommand(OFF)
            SZ_Deckenlicht.sendCommand(OFF)
            Lights.members.filter[item | item.state == NULL].forEach[item | item.sendCommand(OFF)]
        }
        case 3: {
            Lights.members.filter[item | item.state != ON].forEach[item | item.sendCommand(ON)]
            logInfo(filename, "Lichtprogramm 3 - Voll")
        }
    }
end


// Homematic 8-Taster Fernbedienung Wohnzimmer
rule "Fernbedienung Regel 1"
when
    Item Fernbedienung_Taste1 received update ON
then
    logInfo (filename, "Fernbedienung 1.Reihe links")
    WZ_Origami.sendCommand(ON)
    WZ_Lacks_LEDs.sendCommand(ON)
    WZ_Expedit.sendCommand(ON)
end
rule "Fernbedienung Regel 2"
when
    Item Fernbedienung_Taste2 received update ON
then
    logInfo (filename, "Fernbedienung 1.Reihe rechts")
    WZ_Origami.sendCommand(OFF)
    WZ_Lacks_LEDs.sendCommand(OFF)
    WZ_Expedit.sendCommand(OFF)
end
rule "Fernbedienung Regel 3"
when
    Item Fernbedienung_Taste3 received update ON
then
    logInfo (filename, "Fernbedienung 2.Reihe links")
    BK_Balkonmagic.sendCommand(ON)
end
rule "Fernbedienung Regel 4"
when
    Item Fernbedienung_Taste4 received update ON
then
    logInfo (filename, "Fernbedienung 2.Reihe rechts")
    BK_Balkonmagic.sendCommand(OFF)
end
rule "Fernbedienung Regel 5"
when
    Item Fernbedienung_Taste5 received update ON
then
    logInfo (filename, "Fernbedienung 3.Reihe links")
    FK_Arbeit.sendCommand(ON)
    FK_Edison_Dimmer_Level.sendCommand(70)
end
rule "Fernbedienung Regel 6"
when
    Item Fernbedienung_Taste6 received update ON
then
    logInfo (filename, "Fernbedienung 3.Reihe rechts")
    FK_Arbeit.sendCommand(OFF)
    FK_Edison_Dimmer_Level.sendCommand(0)
end
rule "Fernbedienung Regel 7"
when
    Item Fernbedienung_Taste7 received update ON
then
    logInfo (filename, "Fernbedienung 4.Reihe links")
    Whg_Light_Preset.sendCommand(2)
end
rule "Fernbedienung Regel 8"
when
    Item Fernbedienung_Taste8 received update ON
then
    logInfo (filename, "Fernbedienung 4.Reihe rechts")
    Whg_Light_Preset.sendCommand(0)
end


// Homematic Schlüsselanhänger
rule "Schlüsselanhänger Taste 1"
when
    Item Keychain_Taste1 changed to ON
then
    logInfo (filename, "Schlüsselanhänger Taste 1 - Haus")
end
rule "Schlüsselanhänger Taste 2"
when
    Item Keychain_Taste2 changed to ON
then
    logInfo (filename, "Schlüsselanhänger Taste 2 - Haus mit Person")
end
rule "Schlüsselanhänger Taste 3"
when
    Item Keychain_Taste3 changed to ON
then
    logInfo (filename, "Schlüsselanhänger Taste 3 - Ein/Aus")
    Whg_Light_Preset.sendCommand(0)
end
rule "Schlüsselanhänger Taste 4"
when
    Item Keychain_Taste4 changed to ON
then
    logInfo (filename, "Schlüsselanhänger Taste 4 - Licht")
    Whg_Light_Preset.sendCommand(2)
end


// Flurtaster links (Fuse) und rechts (Tokyo)
rule "Flur beide Taster oben kurz"
  when Item FK_Taster_Tokyo_2Short changed to ON or
       Item FK_Taster_Fuse_2Short changed to ON
  then Whg_Light_Preset.sendCommand(2)
end
rule "Flur beide Taster oben lang"
  when Item FK_Taster_Tokyo_2Long changed to ON or
       Item FK_Taster_Fuse_2Long changed to ON
  then Whg_Light_Preset.sendCommand(1)
end
rule "Flur beide Taster unten kurz"
  when Item FK_Taster_Tokyo_1Short changed to ON or
       Item FK_Taster_Fuse_1Short changed to ON
  then Whg_Light_Preset.sendCommand(0)
end
rule "Flur beide Taster unten lang"
  when Item FK_Taster_Tokyo_1Long changed to ON or
       Item FK_Taster_Fuse_1Long changed to ON
  then Whg_Light_Preset.sendCommand(0)
end

// vim: syntax=Xtend
